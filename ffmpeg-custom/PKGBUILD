# Maintainer: nyfair <nyfair2012@gmail.com>
pkgname=(ffmpeg-static ffmpeg-shared)
pkgver=4.2
pkgrel=1
pkgdesc="Complete and free Internet live audio and video solution"
_zlibver=1.2.11
_lamever=3.100
_oggver=1.3.3
_vorbisver=1.3.6
_opusver=1.3.1
_dav1dver=0.4.0
arch=('x86_64')
url="http://ffmpeg.org"
provides=('ffmpeg')
license=('LGPL')
source=("https://zlib.net/current/zlib-$_zlibver.tar.gz"
        "http://downloads.sourceforge.net/lame/lame-$_lamever.tar.gz"
        "http://downloads.xiph.org/releases/ogg/libogg-$_oggver.tar.xz"
        "https://github.com/AO-Yumi/vorbis_aotuv/archive/beta6.03-2018.tar.gz"
        "http://downloads.xiph.org/releases/opus/opus-$_opusver.tar.gz"
        "https://code.videolan.org/videolan/dav1d/-/archive/$_dav1dver/dav1d-$_dav1dver.tar.bz2"
        "http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.xz")
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

build() {
  TMP="${srcdir}/tmp"
  mkdir -p ${TMP}
  FLAGS="-march=core2 -O3 -pipe -ffat-lto-objects"
  export CFLAGS=${FLAGS}
  export CXXFLAGS=${FLAGS}
  export PKG_CONFIG_PATH=${TMP}/lib/pkgconfig

  cd "${srcdir}/zlib-$_zlibver/"
  CC=gcc ./configure --prefix=${TMP} --static
  make install

  cd "${srcdir}/lame-$_lamever/"
  ./configure prefix=${TMP} --disable-shared --enable-nasm --disable-frontend
  CFLAGS="-msse2 ${CFLAGS}" make install

  cd "${srcdir}/libogg-$_oggver/"
  ./configure prefix=${TMP} --disable-shared
  make install

  cd "${srcdir}/vorbis_aotuv-beta6.03-2018"
  ./configure prefix=${TMP} --disable-shared --disable-docs --disable-examples
  CFLAGS="-I${TMP}/include ${CFLAGS}" LDFLAGS="-L${TMP}/lib ${LDFLAGS}" make install

  cd "${srcdir}/opus-$_opusver/"
  ./configure prefix=${TMP} --disable-shared --disable-doc --disable-extra-programs
  make install

  cd "${srcdir}/dav1d-$_dav1dver/"
  meson build --prefix ${TMP} --buildtype release --default-library static
  ninja -C build install

  unset CFLAGS
  CF="-I${TMP}/include -I${TMP}/include/opus ${FLAGS}"
  LF="-L${TMP}/lib ${LDFLAGS}"
  CARGS="--target-os=mingw32 --arch=x86_64 --disable-debug --disable-stripping --disable-doc
    --enable-gpl --enable-version3 --enable-nonfree --enable-zlib
    --disable-ffplay --disable-ffprobe --disable-avdevice
    --enable-libmp3lame --enable-libvorbis --enable-libopus --enable-libdav1d 
    --disable-decoder=libvorbis --disable-decoder=libopus"
}

package_ffmpeg-static() {
  cd "${srcdir}/ffmpeg-$pkgver/"
  ./configure prefix=/usr --extra-cflags="${CF}" --extra-ldflags="${LF}" ${CARGS} --disable-shared
  make
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/bin/*.lib ${pkgdir}/usr/share ${pkgdir}/usr/lib ${pkgdir}/usr/include
}

package_ffmpeg-shared() {
  cd "${srcdir}/ffmpeg-$pkgver/"
  ./configure prefix=/usr --extra-cflags="${CF}" --extra-ldflags="${LF}" ${CARGS} --enable-shared
  make
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/bin/*.lib ${pkgdir}/usr/share ${pkgdir}/usr/lib/*.def ${pkgdir}/usr/lib/pkgconfig
  cp -r ${TMP}/include/* ${pkgdir}/usr/include/
  cp -r ${TMP}/lib/* ${pkgdir}/usr/lib/
}