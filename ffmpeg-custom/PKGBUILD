# Maintainer: nyfair <nyfair2012@gmail.com>

pkgname=ffmpeg-custom
pkgver=3.4
pkgrel=1
pkgdesc="Complete and free Internet live audio and video solution"
_lamever=3.100
_fdkver=0.1.5
arch=('x86_64')
url="http://ffmpeg.org"
provides=('ffmpeg')
makedepends=('yasm')
license=('LGPL')
source=("http://downloads.sourceforge.net/lame/lame-$_lamever.tar.gz"
        "http://downloads.sourceforge.net/opencore-amr/fdk-aac/fdk-aac-$_fdkver.tar.gz"
        "http://ffmpeg.org/releases/ffmpeg-$pkgver.tar.xz"
        extra.7z)
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

build() {
  HOST=x86_64-w64-mingw32
  CHOST="--build=$HOST --host=$HOST --target=$HOST"
  TMP="${srcdir}/tmp"
  mkdir -p ${TMP}
  FLAGS="-march=core2 -O3 -pipe -ffat-lto-objects"
  export CFLAGS=${FLAGS}
  export CXXFLAGS=${FLAGS}

  cd "${srcdir}/lame-$_lamever/"
  ./configure ${CHOST} prefix=${TMP} --disable-shared
  CFLAGS="-msse2 ${CFLAGS}" make install

  cd "${srcdir}/fdk-aac-$_fdkver/"
  ./configure ${CHOST} prefix=${TMP} --disable-shared CXXFLAGS="-Wno-narrowing ${CXXFLAGS}"
  make install

  unset CFLAGS
  cd "${srcdir}/ffmpeg-$pkgver/"
  cp -r ${srcdir}/CL ${TMP}/include
  cp ${srcdir}/libOpenCL.a ${TMP}/lib/
  ./configure prefix=/usr --extra-cflags="-I${TMP}/include ${FLAGS}" \
    --extra-ldflags="-L${TMP}/lib ${LDFLAGS}" --target-os=mingw32 --arch=x86_64 --disable-debug \
    --disable-stripping --disable-static --enable-shared --disable-doc \
    --enable-gpl --enable-version3 --enable-nonfree --enable-opencl \
    --disable-ffplay --disable-ffprobe --disable-ffserver --disable-avdevice \
    --enable-libmp3lame --enable-libfdk_aac --disable-encoders \
    --enable-encoder=rawvideo --enable-encoder=flac --enable-encoder=libmp3lame \
    --enable-encoder=pcm_s8 --enable-encoder=pcm_s16le --enable-encoder=pcm_s24le \
    --enable-encoder=pcm_s32le --enable-encoder=pcm_f32le  --enable-encoder=libfdk_aac
  make
}

package() {
  cd "${srcdir}/ffmpeg-$pkgver/"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/bin/*.lib ${pkgdir}/usr/share ${pkgdir}/usr/lib/*.def ${pkgdir}/usr/lib/pkg*
}
